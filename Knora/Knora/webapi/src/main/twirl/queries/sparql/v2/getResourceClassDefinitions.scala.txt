@*
 * Copyright © 2015 Lukas Rosenthaler, Benjamin Geer, Ivan Subotic,
 * Tobias Schweizer, André Kilchenmann, and Sepideh Alassi.
 *
 * This file is part of Knora.
 *
 * Knora is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published
 * by the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Knora is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public
 * License along with Knora.  If not, see <http://www.gnu.org/licenses/>.
 *@

@import org.knora.webapi.IRI

@**
 * Gets all the resource class definitions.
 *
 * @param triplestore the name of the triplestore being used.
 *@
@(triplestore: String)

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX knora-base: <http://www.knora.org/ontology/knora-base#>
PREFIX salsah-gui: <http://www.knora.org/ontology/salsah-gui#>

SELECT ?graph ?resourceClass ?resourceClassPred ?resourceClassObj ?resourceClassObjLang ?cardinalityProp ?cardinality ?cardinalityVal ?guiOrder
@* Ensure that inference is not used in this query. *@
@if(triplestore.startsWith("graphdb")) {
    FROM <http://www.ontotext.com/explicit>
}
WHERE {
    GRAPH ?graph {
        ?resourceClass rdf:type owl:Class .
    }

    ?resourceClass rdfs:subClassOf* knora-base:Resource .

    {
        ?resourceClass rdfs:subClassOf ?restriction .
        ?restriction rdf:type owl:Restriction .
        ?restriction owl:onProperty ?cardinalityProp .
        ?cardinalityProp rdfs:subPropertyOf* knora-base:resourceProperty .
        ?restriction ?cardinality ?cardinalityVal .

        OPTIONAL {
            ?restriction salsah-gui:guiOrder ?guiOrder .
        }

        FILTER(?cardinality = owl:cardinality || ?cardinality = owl:minCardinality || ?cardinality = owl:maxCardinality)
    }
    UNION
    {
        ?resourceClass ?resourceClassPred ?resourceClassObj .
        FILTER(?resourceClassPred != rdf:type && !isBlank(?resourceClassObj))
        BIND(lang(?resourceClassObj) as ?resourceClassObjLang)
    }
}
