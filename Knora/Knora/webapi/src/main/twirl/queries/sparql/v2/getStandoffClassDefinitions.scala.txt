@*
 * Copyright © 2015 Lukas Rosenthaler, Benjamin Geer, Ivan Subotic,
 * Tobias Schweizer, André Kilchenmann, and Sepideh Alassi.
 *
 * This file is part of Knora.
 *
 * Knora is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published
 * by the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * Knora is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public
 * License along with Knora.  If not, see <http://www.gnu.org/licenses/>.
 *@

@import org.knora.webapi.IRI

@**
 * Gets all the standoff class definitions.
 *
 * @param triplestore the name of the triplestore being used.
 *@
 @(triplestore: String)

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX knora-base: <http://www.knora.org/ontology/knora-base#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>

SELECT ?graph ?standoffClass ?standoffClassPred ?standoffClassObj ?standoffClassObjLang ?cardinalityProp ?cardinality ?cardinalityVal
@* Ensure that inference is not used in this query. *@
@if(triplestore.startsWith("graphdb")) {
    FROM <http://www.ontotext.com/explicit>
}
WHERE {

    GRAPH ?graph {
        ?standoffClass rdf:type owl:Class .
    }

    ?standoffClass rdfs:subClassOf* knora-base:StandoffTag .

	# not all standoff classes define additional cardinalities to those defined for knora-base:StandoffTag
    OPTIONAL {
        ?standoffClass rdfs:subClassOf ?restriction .
        ?restriction rdf:type owl:Restriction .
        ?restriction owl:onProperty ?cardinalityProp .
        ?cardinalityProp rdfs:subPropertyOf+ knora-base:objectCannotBeMarkedAsDeleted .
        ?restriction ?cardinality ?cardinalityVal .
        FILTER(?cardinality = owl:cardinality || ?cardinality = owl:minCardinality || ?cardinality = owl:maxCardinality)
    }

    {
        ?standoffClass ?standoffClassPred ?standoffClassObj .
        FILTER(?standoffClassPred != rdf:type && !isBlank(?standoffClassObj))
        BIND(lang(?standoffClassObj) as ?standoffClassObjLang)
    }
}